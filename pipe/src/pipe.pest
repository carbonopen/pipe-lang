pipe = _{ SOI ~ sessions* ~ EOI }
WHITESPACE = _{ " " | "\t" | "\r" | "\r\n" | "\n"  }
COMMENT = _{ (comment_block | comment_line ) }

comment_block = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
comment_line = _{ "//" ~ (!"//" ~ ANY)*  }

// Session
sessions = { (session_generic_config | session_pipeline)+ }

session_generic_config = { ident ~ session_generic_config_content }
session_pipeline = { ident ~ session_pipeline_content }

session_generic_config_content = { "{" ~ (common_content)* ~ "}" }
session_pipeline_content = { "{" ~ (module)* ~ "}" }

//Modules
module = { ident ~ ident? ~ module_content }
module_content = { "{" ~ (common_content | attach)* ~ "}" }

//Attatch
attach= { "attach" ~ (module | ident) }

//Macro
param_macro = { ident ~ (value)*? ~ param_macro_content }
param_macro_content = { "(" ~ common_content* ~ ")" }

common_content = { (param | param_macro)+  }

// Param
param = { ident ~ "=" ~ value }

// Ident
ident = @{ (letter | "_") ~ (ident_char)* }
ident_char = _{ letter | digit | "-" | "." | "_" }
// ident_first_char = _{ letter | "_" }
letter = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
// name = _{ string }

// Value
value = _{ object | array | string | number | boolean | null | interpolation | string_interpolation  }

//interpolation
interpolation = { "${" ~ interpolation_content  ~ "}" }
interpolation_content = @{ (!"}" ~ ANY)* }
string_interpolation = @{ "`" ~ string_interpolation_content ~ "`" }
string_interpolation_content = @{ (!"`" ~ ANY)* }

// Var types
string = @{ "\"" ~ string_content ~ "\"" }
string_content = @{ (!"\"" ~ ANY)* }
boolean = { "true" | "false" }
null = { "null" }
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ "]"
}
object = {
    "{" ~ "}" |
    "{" ~ pair ~ ("," ~ pair)* ~ "}"
}
pair = { string ~ ":" ~ value }

